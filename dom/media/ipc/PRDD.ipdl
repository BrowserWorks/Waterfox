/* -*- Mode: C++; tab-width: 8; indent-tabs-mode: nil; c-basic-offset: 2 -*- */
/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

include GraphicsMessages;
include MemoryReportTypes;
include PrefsTypes;

include protocol PProfiler;
include protocol PRemoteDecoderManager;
include protocol PVideoBridge;

using mozilla::dom::NativeThreadId from "mozilla/dom/TabMessageUtils.h";
using moveonly mozilla::UntrustedModulesData from "mozilla/UntrustedModulesData.h";
using moveonly mozilla::ModulePaths from "mozilla/UntrustedModulesData.h";
using moveonly mozilla::ModulesMapResult from "mozilla/UntrustedModulesData.h";

namespace mozilla {

// This protocol allows the UI process to talk to the RDD
// (RemoteDataDecoder) process. There is one instance of this protocol,
// with the RDDParent living on the main thread of the RDD process and
// the RDDChild living on the main thread of the UI process.
protocol PRDD
{
parent:

  async Init(GfxVarUpdate[] vars, FileDescriptor? sandboxBroker,
             bool canRecordReleaseTelemetry);

  async InitProfiler(Endpoint<PProfilerChild> endpoint);

  async NewContentRemoteDecoderManager(
            Endpoint<PRemoteDecoderManagerParent> endpoint);

  async RequestMemoryReport(uint32_t generation,
                            bool anonymize,
                            bool minimizeMemoryUsage,
                            FileDescriptor? DMDFile);

  async PreferenceUpdate(Pref pref);

  async UpdateVar(GfxVarUpdate var);

  async InitVideoBridge(Endpoint<PVideoBridgeChild> endpoint);

  async GetUntrustedModulesData() returns (UntrustedModulesData? data);

child:

  async InitCrashReporter(NativeThreadId threadId);

  async AddMemoryReport(MemoryReport aReport);
  async FinishMemoryReport(uint32_t aGeneration);

  async GetModulesTrust(ModulePaths aModPaths, bool aRunAtNormalPriority)
      returns (ModulesMapResult? modMapResult);
};

} // namespace mozilla
