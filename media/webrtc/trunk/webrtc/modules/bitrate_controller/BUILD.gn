# Copyright (c) 2014 The WebRTC project authors. All Rights Reserved.
#
# Use of this source code is governed by a BSD-style license
# that can be found in the LICENSE file in the root of the source
# tree. An additional intellectual property rights grant can be found
# in the file PATENTS.  All contributing project authors may
# be found in the AUTHORS file in the root of the source tree.

import("../../build/webrtc.gni")

rtc_static_library("bitrate_controller") {
  # TODO(mbonadei): Remove (bugs.webrtc.org/6828)
  # Errors on cyclic dependency with:
  # congestion_controller:congestion_controller if enabled.
  check_includes = false

  sources = [
    "bitrate_controller_impl.cc",
    "bitrate_controller_impl.h",
    "include/bitrate_controller.h",
    "send_side_bandwidth_estimation.cc",
    "send_side_bandwidth_estimation.h",
  ]

  if (rtc_enable_bwe_test_logging) {
    defines = [ "BWE_TEST_LOGGING_COMPILE_TIME_ENABLE=1" ]
  } else {
    defines = [ "BWE_TEST_LOGGING_COMPILE_TIME_ENABLE=0" ]
  }

  # TODO(jschuh): Bug 1348: fix this warning.
  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]

  if (!build_with_chromium && is_clang) {
    # Suppress warnings from the Chromium Clang plugin (bugs.webrtc.org/163).
    suppressed_configs += [ "//build/config/clang:find_bad_constructs" ]
  }

  deps = [
    "../../base:rtc_base_approved",
    "../../system_wrappers",
    "../rtp_rtcp",
  ]
}
