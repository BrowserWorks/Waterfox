# Copyright (c) 2014 The WebRTC project authors. All Rights Reserved.
#
# Use of this source code is governed by a BSD-style license
# that can be found in the LICENSE file in the root of the source
# tree. An additional intellectual property rights grant can be found
# in the file PATENTS.  All contributing project authors may
# be found in the AUTHORS file in the root of the source tree.

import("../../build/webrtc.gni")

rtc_static_library("congestion_controller") {
  sources = [
    "congestion_controller.cc",
    "delay_based_bwe.cc",
    "delay_based_bwe.h",
    "include/congestion_controller.h",
    "median_slope_estimator.cc",
    "median_slope_estimator.h",
    "probe_bitrate_estimator.cc",
    "probe_bitrate_estimator.h",
    "probe_controller.cc",
    "probe_controller.h",
    "probing_interval_estimator.cc",
    "probing_interval_estimator.h",
    "transport_feedback_adapter.cc",
    "transport_feedback_adapter.h",
    "trendline_estimator.cc",
    "trendline_estimator.h",
  ]

  if (rtc_enable_bwe_test_logging) {
    defines = [ "BWE_TEST_LOGGING_COMPILE_TIME_ENABLE=1" ]
  } else {
    defines = [ "BWE_TEST_LOGGING_COMPILE_TIME_ENABLE=0" ]
  }

  # TODO(jschuh): Bug 1348: fix this warning.
  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]

  if (!build_with_chromium && is_clang) {
    # Suppress warnings from the Chromium Clang plugin (bugs.webrtc.org/163).
    suppressed_configs += [ "//build/config/clang:find_bad_constructs" ]
  }

  deps = [
    "../..:webrtc_common",
    "../../base:rtc_base",
    "../../base:rtc_base_approved",
    "../../base:rtc_numerics",
    "../../system_wrappers",
    "../bitrate_controller",
    "../pacing",
    "../remote_bitrate_estimator",
    "../rtp_rtcp",
    "../utility",
  ]
}
